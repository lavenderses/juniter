import com.github.spotbugs.snom.SpotBugsTask

plugins {
    id 'java'
    id 'com.diffplug.spotless' version '6.2.1'
    id 'jacoco'
    id 'checkstyle'
    id 'com.github.spotbugs' version '5.0.5'
}

group 'com.naknaoi.juniter'
version '0.1.0'

sourceCompatibility = '11'
targetCompatibility = '11'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.8.2'
    testImplementation 'org.assertj:assertj-core:3.22.0'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'org.yaml:snakeyaml:1.30'
    testImplementation 'org.mockito:mockito-core:4.3.1'
    testImplementation 'com.h2database:h2:2.1.210'
    testImplementation 'org.dbunit:dbunit:2.7.3'
    testImplementation 'com.google.errorprone:error_prone_annotations:2.11.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'

    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.11.0'
}

test {
    useJUnitPlatform {
        if (BUILT_BY == "Jenkins") {
            excludeTags 'slow-test', 'DB'
        } else {
            excludeTags 'DB'
        }
    }
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
}

spotless {
    format 'misc', {
        target '*.gradle'
        indentWithSpaces()
        trimTrailingWhitespace()
        endWithNewline()
    }
    java {
        importOrder()
        removeUnusedImports()

        String formatVersion = '1.13.0'
        googleJavaFormat formatVersion
    }
}

checkstyle {
    toolVersion = '8.45'
}

tasks.withType(SpotBugsTask) {
    reports {
        xml.required.set(true)
        html.required.set(true)
    }
}
